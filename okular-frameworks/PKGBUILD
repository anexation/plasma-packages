# Maintainer: anex <lane.wiscombe[@]gmail.com>

pkgname=okular-frameworks
pkgver=5.1.99
pkgrel=1
pkgdesc='Universal document viewer'
arch=('i686' 'x86_64')
url='http://kde.org/applications/graphics/okular/'
license=('GPL')
depends=('kpty' 'kdelibs4support' 'threadweaver' 'kactivities-frameworks' 'khtml' 'chmlib' 'djvulibre' 'libspectre' 'poppler-qt5' 
'libkscreen-frameworks' 'libkexiv2-frameworks-git')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'python' 'ebook-tools' 'qca-qt5')
optdepends=('ebook-tools: mobi and epub support' 'qca-qt5: support for encrypted ODF documents')
conflicts=('kdegraphics-okular' 'okular' 'okular-frameworks-git')
provides=('okular' 'okular-frameworks' 'okular-frameworks-git')
replaces=('okular-frameworks-git')
source=("git://anongit.kde.org/okular.git#branch=frameworks")
install=$pkgname.install
sha256sums=('SKIP')

prepare() {
  mkdir -p build

  sed -e 's|find_package(Qca|find_package(Qca-qt5|g' -e 's|Qca_FOUND|Qca-qt5_FOUND|g' -i okular/generators/ooo/CMakeLists.txt
}

build() { 
  cd build
  cmake ../okular \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
