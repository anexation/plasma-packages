# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=skanlite-frameworks-git
pkgver=r206.02c87e6
pkgrel=1
pkgdesc="A Lite image scanning application. KF5 Frameworks branch (GIT version)"
arch=('i686' 'x86_64')
url="http://www.kde.org/applications/graphics/skanlite"
license=('GPL')
depends=('libksane-frameworks-git' 'kio')
makedepends=('cmake' 'git' 'extra-cmake-modules' 'python')
conflicts=('skanlite')
provides=('skanlite')
source=('git://anongit.kde.org/skanlite#branch=frameworks')
sha1sums=('SKIP')
_gitname="skanlite"

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() { 
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
