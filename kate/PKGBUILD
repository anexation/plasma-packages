# Maintainer: anex <lane.wiscombe[@]gmail.com>

pkgname=kate
pkgver=14.2.1
pkgrel=1
pkgdesc='An advanced editor component which is used in numerous KDE applications requiring a text editing component'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/applications/kate'
license=('LGPL')
depends=('knewstuff' 'ktexteditor' 'plasma-framework' 'threadweaver')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
provides=('kate' 'kate-git')
conflicts=('kate' 'kdesdk-kate' 'kdebase-kwrite' 'kate-git')
replaces=('kate-git')
install=kate.install
source=('git://anongit.kde.org/kate.git')
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kate \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
