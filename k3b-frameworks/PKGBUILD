# Maintainer: anex <lane.wiscombe[@]gmail.com>

pkgname=k3b-frameworks
pkgver=2.9.99
pkgrel=1
pkgdesc='Feature-rich and easy to handle CD burning application. KF5 Frameworks branch'
arch=('i686' 'x86_64')
license=('GPL')
url='http://k3b.sourceforge.net'
depends=('qt5-webkit' 'kfilemetadata5' 'knotifyconfig' 'kcmutils' 'libkcddb-frameworks' 'desktop-file-utils' 'libsamplerate')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'kdoctools' 'flac' 'libmpcdec' 'ffmpeg' 'libmad' 'libdvdread' 'musicbrainz' 'libvorbis')
install="${pkgname}.install"
optdepends=('cdrdao: for CD DAO mode burning support'
            'cdrkit: for CD burning support'
            'cdrtools: for CD burning support'
            'dvd+rw-tools: for DVD burning support'
            'normalize: for WAV normalization'
            'vcdimager: for VCD burning support'
            'transcode: for advanced MPEG conversion support'
            'sox: for encode audio files in formats such as AIFF or VOC'
            'emovix: for bootable multimedia CD/DVD support'
            'taglib: Read and write tags in audio files'
            'flac: Needed for the Flac audio decoder plugin'
            'libmpcdec: Needed for the Musepack audio decoder plugin'
            'ffmpeg: Needed for the K3b FFmpeg decoder plugin which can decode virtually all audio types'
            'libmad: Needed for the mp3 audio decoder plugin'
            'libsndfile: Needed for the libsndfile audio decoder plugin'
            'libdvdread: Reading DVD video disks'
            'lame: Needed for the lame mpf encoder encoder plugin'
            'libvorbis: Needed for the K3b Ogg Vorbis decoder and encoder plugins'
            'musicbrainz: Provide information about the CD, about the artist or about related information' )
options=('!libtool')
provides=('k3b' 'k3b-frameworks' 'k3b-frameworks-git')
conflicts=('k3b' 'k3b-frameworks-git')
replaces=('k3b-frameworks-git')
source=('git://anongit.kde.org/k3b.git#branch=kf5')
sha1sums=('SKIP')
_pkgname="k3b"

prepare() {
  mkdir -p build
  sed '261iinstall( FILES  k3b.notifyrc DESTINATION ${KNOTIFYRC_INSTALL_DIR})' -i k3b/src/CMakeLists.txt
  sed 's|k3b.notifyrc DESTINATION ${DATA_INSTALL_DIR}/k3b|DESTINATION ${KXMLGUI_INSTALL_DIR}/k3b|g' -i k3b/src/CMakeLists.txt
}

build() {
  cd build
  cmake "../${_pkgname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
