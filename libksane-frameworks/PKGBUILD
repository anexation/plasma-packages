# Maintainer: anex <lane.wiscombe[@]gmail.com>

pkgname=libksane-frameworks
pkgver=14.12.1
pkgrel=1
pkgdesc='A image scanning library that provides a QWidget that contains all the logic needed to interface a sacanner. KF5 Frameworks branch.'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdegraphics/libs/libksane'
license=('LGPL')
depends=('kwallet' 'sane' 'ktextwidgets')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'cmake')
conflicts=('libksane' 'libksane-frameworks-git')
provides=('libksane' 'libksane-frameworks' 'libksane-frameworks-git')
replaces=('libksane-frameworks-git')
source=('git://anongit.kde.org/libksane#branch=frameworks')
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libksane \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}