# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-desktop-git
pkgver=r1357.42cd0a7
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings-git' 'ksysguard-git' 
'powerdevil-git' 'kmenuedit-git' 'kinfocenter-git' 'polkit-kde-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=('plasma-nm-git: Network manager applet')
conflicts=('plasma-desktop' 'kdebase-workspace' 'kdebase-kdepasswd')
provides=('plasma-desktop')
install=$pkgname.install
source=('git://anongit.kde.org/plasma-desktop.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-desktop
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-desktop \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
