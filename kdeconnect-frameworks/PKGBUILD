# Maintainer: anex <lane.wiscombe[@]gmail.com>

pkgname=kdeconnect-frameworks
pkgver=0.7.3
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/base/kdeconnect-kde'
license=('GPL2')
depends=('kcmutils' 'kio' 'qca-qt5' 'libfakekey')
makedepends=('extra-cmake-modules' 'git' 'python')
provides=('kdeconnect-frameworks' 'kdeconnect-frameworks-git')
conflicts=('kdeconnect-frameworks-git' 'kdeconnect')
replaces=('kdeconnect-frameworks-git')
source=("git://anongit.kde.org/kdeconnect-kde.git#branch=frameworks")
install=$pkgname.install
sha256sums=('SKIP')

prepare() {
  mkdir -p build

 sed 's|(Qca REQUIRED)|(Qca-qt5 REQUIRED)|' -i kdeconnect-kde/CMakeLists.txt
 sed 's|qca|qca-qt5|g' -i kdeconnect-kde/core/CMakeLists.txt -i kdeconnect-kde/plugins/sftp/CMakeLists.txt
}

build() {
  cd build
  cmake ../kdeconnect-kde \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
