# Maintainer: anex <lane.wiscombe[@]gmail.com>

pkgname=breeze-kde4
pkgver=5.2.90
pkgrel=1
pkgdesc='Breeze widget style for KDE4 applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
depends=('kdelibs')
makedepends=('extra-cmake-modules' 'git' 'automoc4' 'ki18n')
conflicts=('breeze-kde4-git')
provides=('breeze-kde4' 'breeze-kde4-git')
replaces=('breeze-kde4-git')
source=('git://anongit.kde.org/breeze.git')
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../breeze \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# needed so that pure Qt4 apps are correctly themed
  install -d -m755 "$pkgdir"/usr/lib/qt4/plugins/styles
  ln -sr "$pkgdir"/usr/lib/kde4/plugins/styles/breeze.so "$pkgdir"/usr/lib/qt4/plugins/styles
}
